<?xml version="1.0" encoding="ISO-8859-15" standalone="no"?><uc-export clientvers="11.0.0">
<JOBI name="PCK.AUTOMIC_ORACLE_DB.PRV.INCLUDE.CREATE_TABLESPACE">
<HEADER state="1">
<Title/>
<ArchiveKey1/>
<ArchiveKey2/>
<OH_SubType/>
</HEADER>
<SCRIPT mode="1" state="1">
<MSCRI><![CDATA[:SET &UC4RB_DB_SCRIPT_SEPR# = ";"
:SET &UC4RB_SQL_SEPARATOR# = ","
:SET &UC4RB_SQL_CONTENT# = "create "
:SET &UC4RB_SQL_PASSWORD# = ""
:IF &UC4RB_DB_SMALLFILE# = "SMALLFILE"
:  SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, "smallfile")
:ELSE
:  IF &UC4RB_DB_SMALLFILE# = "BIGFILE"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, "bigfile")
:  ENDIF
:ENDIF

:IF &UC4RB_DB_TABLESPACE_TYPE# = "Permanent"
:  SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " tablespace &UC4RB_DB_TABLESPACE_NAME#")
:  IF &UC4RB_DB_PERM_DATAFILE_SPEC# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " datafile '&UC4RB_DB_PERM_DATAFILE_SPEC#'")
:  ENDIF

:  IF &UC4RB_DB_MIN_EXT_SIZE# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " minimum extent &UC4RB_DB_MIN_EXT_SIZE#")
:  ENDIF

:  IF &UC4RB_DB_BLOCK_SIZE# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " blocksize &UC4RB_DB_BLOCK_SIZE#")
:  ENDIF

:  IF &UC4RB_DB_LOGGING# = "YES"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " logging")
:  ELSE
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " nologging")
:  ENDIF

:  IF &UC4RB_DB_FORCE_LOGGING# = "YES"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " force logging")
:  ENDIF

:  IF &UC4RB_DB_DFLT_STRG_CLAUSE# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " default &UC4RB_DB_DFLT_STRG_CLAUSE#")
:  ENDIF

:  IF &UC4RB_DB_ONLINE# = "YES"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " online")
:  ELSE
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " offline")
:  ENDIF
:  IF &UC4RB_DB_SEGM_SPC_AUTO# = "YES"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " segment space management auto")
:  ELSE
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, "  segment space management manual")
:  ENDIF
:ENDIF

:IF &UC4RB_DB_TABLESPACE_TYPE# = "Temporary"
:  SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " temporary tablespace &UC4RB_DB_TABLESPACE_NAME#")
:  IF &UC4RB_DB_TEMPFILE_SPEC# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " tempfile '&UC4RB_DB_TEMPFILE_SPEC#'")
:  ENDIF

:  IF &UC4RB_DB_TS_GROUP_NAME# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " tablespace group '&UC4RB_DB_TS_GROUP_NAME#'")
:  ENDIF
:ENDIF

:IF &UC4RB_DB_TABLESPACE_TYPE# = "Undo"
:  SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " undo tablespace &UC4RB_DB_TABLESPACE_NAME#")
:  IF &UC4RB_DB_UNDO_DATAFILE_SPEC# <> ""
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " datafile '&UC4RB_DB_UNDO_DATAFILE_SPEC#'")
:  ENDIF

:  IF &UC4RB_DB_RETENTION# = "YES"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " retention guarantee")
:  ELSE
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " retention noguarantee")
:  ENDIF
:ENDIF

:IF &UC4RB_DB_EXT_MGMT_CLAUSE# = "Generate"
:  SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " extent management")
:  IF &UC4RB_DB_EXT_MGMT_TYPE# = "Local"
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " local")
:    IF &UC4RB_DB_EXT_MGMT_AUTOALLOC# = "YES"
:      SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " autoallocate")
:    ELSE
:      IF &UC4RB_DB_EXT_MGMT_UNIFORM# = "YES"
:        SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " uniform")
:        IF &UC4RB_DB_EXT_UNFRM_MGMT_SIZE# <> ""
:          SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " size &UC4RB_DB_EXT_UNFRM_MGMT_SIZE#")
:        ENDIF
:      ENDIF
:    ENDIF
:  ELSE
:    SET &UC4RB_SQL_CONTENT# = STR_CAT(&UC4RB_SQL_CONTENT#, " dictionary")
:  ENDIF
:ENDIF

:SET &UC4RB_SQL_DATABASENAME# = ""

:INCLUDE PCK.AUTOMIC_ORACLE_DB.PRV.INCLUDE.CREATE_SQL_SCRIPT_FILE
:INCLUDE PCK.AUTOMIC_ORACLE_DB.PRV.INCLUDE.EXECUTE_SQL_STATEMENT]]></MSCRI>
</SCRIPT>
<DOCU_Docu state="1" type="text">
<DOC/>
</DOCU_Docu>
</JOBI>
</uc-export>