*** Settings ***
Documentation     This is high level keywords that will be used by local test cases
Resource          messages.txt
Resource          context.txt
Library           com.automic.robot.itpa.ItpaLibrary

*** Key Words ***
Init Action
    [Arguments]    ${actionName}
    Action Create    ${actionName}
    Action Set    ${PROMPTSET_AGENT}    ${_AGENT}
    Action Set    ${PROMPTSET_LOGIN}    ${_LOGIN}

Action Report Should Contain
    [Arguments]    ${text}
    ${report} =    Action Get Report
    Should Contain    ${report}    ${text}

Action Report Should Be Found
    ${report} =    Action Get Report
    Should Not Be Empty    ${report}

Action Report Should Not Be Found
    ${report} =    Action Get Report
    Should Be Empty    ${report}

Action Return Code Should Be
    [Arguments]    ${code}
    ${returnCode}=    Action Get Variable    Return code
    Should Be Equal    ${returnCode}    ${code}

Action Return Code Should Not Equal
    [Arguments]    ${code}
    ${returnCode}=    Action Get Variable    Return code
    Should Not Be Equal    ${returnCode}    ${code}

Action Return Status Should Be
    [Arguments]    ${status}
    ${returnStatus}=    Action Get Variable    Status
    Should Be Equal    ${returnStatus}    ${status}

Action Return Variable Should Be
    [Arguments]    ${variableValue}
    ${out}=    Action Get Variable    ${variableName}
    Should Be Equal    ${out}    ${variableValue}

Action Return Variable Should Be Equal As Integers
    [Arguments]    ${variableValue}
    ${out}=    Action Get Variable    ${variableName}
    Should Be Equal As Integers    ${out}    ${variableValue}

Oracle Common Setting
    Action Set    ${PRT_CREATE_USER_DB_USERNAME_LG}    ${DB_USERNAME}
    Action Set    ${PRT_CREATE_USER_DB_PASSWORD_LG}    ${DB_PASSWORD}
    Action Set    ${PRT_CREATE_USER_DB_JDBC_CONN_STRING}    ${CONN_STRING}
    Action Set    ${PRT_CREATE_USER_DB_DRIVER_JAR}    ${JAR}
    Action Set    ${PRT_CREATE_USER_DB_DRIVER_CLASS}    ${CLASS}
    Action Set    ${PRT_CREATE_USER_DB_TNSNAMES_ORA}    ${ORA}
    Action Set    ${PRT_CREATE_USER_DB_CONN_INTRVL}    ${INTRVL}

Create User
    [Arguments]    ${name}    ${externally}    ${globally}    ${password_expire}    ${account_lock}
    Init Action    ${ACTION_CREATE_USER}
    Oracle Common Setting
    Action Set    ${PRT_CREATE_USER_DB_USER_NAME}    ${name}
    Action Set    ${PRT_CREATE_USER_DB_FAIL_USR_EXISTS}    NO
    Action Set    ${PRT_CREATE_USER_DB_USER_PASSWORD}    ${DB_PASSWORD}
    Action Set    ${PRT_CREATE_USER_DB_IDTFT_EXTERNALLY}    ${externally}
    Action Set    ${PRT_CREATE_USER_DB_IDTFT_CERT_DN}    ${EMPTY}
    Action Set    ${PRT_CREATE_USER_DB_IDTFT_GLOBALLY}    ${globally}
    Action Set    ${PRT_CREATE_USER_DB_IDTFT_DIR_DN}    ${EMPTY}
    Action Set    ${PRT_CREATE_USER_DB_DEFAULT_TABLESPACE}    ${EMPTY}
    Action Set    ${PRT_CREATE_USER_DB_TEMP_TABLESPACE}    ${EMPTY}
    Action Set    ${PRT_CREATE_USER_DB_QUOTA_SPEC}    ${EMPTY}
    Action Set    ${PRT_CREATE_USER_DB_PROFILE}    ${EMPTY}
    Action Set    ${PRT_CREATE_USER_DB_PASSWORD_EXPIRE}    ${password_expire}
    Action Set    ${PRT_CREATE_USER_DB_ACCOUNT_LOCK}    ${account_lock}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    0
    Action Return Status Should Be    ${ENDED_OK}

Drop User
    [Arguments]    ${user}    ${cascade}
    Init Action    ${ACTION_DROP_USER}
    Oracle Common Setting
    Action Set    ${PRT_DROP_USER_DB_DROP_USER}    ${user}
    Action Set    ${PRT_DROP_USER_DB_FAIL_USR_NOT_EXISTS}    NO
    Action Set    ${PRT_DROP_USER_DB_CASCADE}    ${cascade}
    Action Execute
    Action Report Should Be Found
    Action Return Code Should Be    0
    Action Return Status Should Be    ${ENDED_OK}

Drop User With Cascade
    [Arguments]    ${user}
    Init Action    ${ACTION_DROP_USER}
    Oracle Common Setting
    Action Set    ${PRT_DROP_USER_DB_DROP_USER}    ${user}
    Action Set    ${PRT_DROP_USER_DB_FAIL_USR_NOT_EXISTS}    NO
    Action Set    ${PRT_DROP_USER_DB_CASCADE}    YES
    Action Execute
